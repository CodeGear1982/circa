

type Node {
    int parent
    List children
    String strategy
    Map props
    any data
}

type Layout {
    List nodes
    Point size
    int cursor
}

def start(Point size) -> Layout
    layout = make(Layout)
    layout.size = size

    -- set up the initial element
    root = make(Node)
    root.parent = -1
    @layout.nodes.append(root)
    layout.cursor = 0

    layout

def Node.setStrategy(@self, String strategy)
    self.nodes[self.cursor].strategy = strategy

def Layout.addNode(self, int cursor, data) -> Layout
    element = make(Node)
    element.data = data
    element.parent = cursor

    newCursor = self.nodes.length

    @self.nodes.append(element)
    @self.nodes[cursor].children.append(newCursor)

    self.cursor = newCursor

    return self

def Layout.modifyCurrentNode(self, Closure f) -> Layout
    @self.nodes.mapItem(self.cursor, f)
    self

def Layout.setProp(self, name, value) -> Layout
    @self.modifyCurrentNode({ 
        node = Node(input())
        @node.props.set(name,value)
    })
    self

def Layout.solveDim(self, String dim) -> List
    -- Return a [(Number,Number)] list of pairs for each node.
    results = empty_list(null, self.nodes.length)
    results[0] = [0 self.size.x]

    for Node node in self.nodes
        availableStart = results[0][0]
        availableFin = results[0][1]
        nextLoc = availableStart

        for int childIndex in node.children
            child = self.nodes[childIndex]
            prop = child.props.get(dim)

            if first_name(prop) == :percent
                percent = prop[1] -> number / 100.0
                desiredSize = (availableFin - availableStart) * percent
            else
                desiredSize = prop -> number
            
            min(@desiredSize, availableFin - nextLoc)

            start = nextLoc
            fin = nextLoc + desiredSize

            results[childIndex] = [start fin]
            nextLoc = fin

    return results

-- Returns a list of layed-out [Rect, any] pairs.
def Layout.getRects(self) -> List
    positions = empty_list(null, self.nodes.length)

    -- Position the root element
    positions[0] = rect(0 0 self.size.x self.size.y)

    x_ranges = self.solveDim(:width)
    y_ranges = self.solveDim(:height)

    for node in self.nodes
        x_range = x_ranges[index()]
        y_range = y_ranges[index()]
        positions[index()] = [x_range[0] y_range[0] x_range[1] y_range[1]]

    positions


-- Test code:

layout = start([100 100])

@layout.addNode(layout.cursor, 1)
@layout.setProp(:width, [:percent 100])
@layout.setProp(:height, [:percent 100])

assert(layout.getRects ==
    [[0 0 100 100] [0 0 100 100]])

layout = start([100 100])

root = layout.cursor

@layout.addNode(root, 1)
@layout.setProp(:width, [:percent 25])
@layout.setProp(:height, [:percent 75])

@layout.addNode(root, 2)
@layout.setProp(:width, [:percent 75])
@layout.setProp(:height, [:percent 25])

assert(layout.getRects ==
    [[0 0 100 100] [0 0 25 75] [25 75 100 100]])

