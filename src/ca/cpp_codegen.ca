
type CodeWriter
{
    int curIndent
    bool needsIndent
    List output
}

def CodeWriter.write(self :out, string s)
    self.output.append(s)

def CodeWriter.writeln(self :out, string line)
    self.output.append(line)
    self.output.append("\n")

def write_type_name(CodeWriter w :out, Term term)
    w.write(term.get_type().name())

def write_top_level(CodeWriter w :out, Branch branch)

    for term in branch.terms()
        @term -> Term

        if term.property('statement') == true
            continue

        if term.function() == comment
            commentStr = term.property('comment')
            if commentStr == ""
                w.writeln('')
            else
                w.writeln(concat('// ' commentStr))
        elif term.function() == value
            -- Value
            w.write('const ')
            write_type_name(w, term)
            w.write(' ')
            w.write(term.name())
            w.write(' = ')
            w.write(term.value() -> to_string)
            w.writeln('')

        else
            -- Normal function call
            termName = term.name()
            if termName != ""
                w.write(concat(termName ' = '))
            w.writeln(concat(term.function().name() '()'))
        


def branch_to_cpp_string(Branch b) -> string
    writer = create(CodeWriter)

    write_top_level(&writer, b)

    return writer.output.join("")

test_branch = {
    a = 1
    b = 2
    -- this is a comment
    add(a 3)
    print(b)
}

branch_to_cpp_string(branch_ref(test_branch)) -> print
