
def is_digit(String s)
  if s.length < 1
    return false
  (s.char_code >= '0'.char_code) and (s.char_code <= '9'.char_code)

struct Reader {
  String str
  int pos
  any last
}

def Reader.empty(self) -> bool
  self.pos >= self.str.length

def Reader.next(self) -> String
  self.str.char_at(self.pos)

def Reader.advance(@self)
  self.pos += 1

def Reader.consumeWhitespace(@self)
  while self.next == ' '
    @self.advance

  self

def Reader.matches(self, str) -> bool
  self.str.substr(self.pos, str.length) == str

def Reader.consume(@self, str)
  self.pos += str.length

def Reader.parse_string(@self) -> Reader
  @self.advance

  p = self.pos
  while self.next != '"'
    if self.empty
      error("Unexpected end of string, looking for: \"")

    -- TODO: handle escapes and unicode
    @self.advance

  result = self.str.slice(p, self.pos)
  @self.advance
  self.last = result

def Reader.parse_value(@self) -> Reader
  @self.consumeWhitespace

  result = nil

  if self.next == '['
    @self.advance
    @self.consumeWhitespace
    
    result = []

    first = true
    while self.next != ']'
      @self.consumeWhitespace
      if self.empty
        error("Unexpected end of string, looking for: ] or ,")

      if not first
        if self.next != ','
          error("Expected: ," self)
        @self.advance
        @self.consumeWhitespace

      @self.parse_value
      @result.append(self.last)
      first = false

      @self.consumeWhitespace

    @self.advance

  if self.next == '{'
    @self.advance
    @self.consumeWhitespace

    result = Table.make

    first = true
    while self.next != '}'
      @self.consumeWhitespace
      if self.empty
        error("Unexpected end of string, looking for: } or ,")

      if not first
        if self.next != ','
          error('Expected ,' self)
        @self.advance
        @self.consumeWhitespace

      @self.parse_string
      key = self.last
      @self.consumeWhitespace

      if self.next != ':'
        error('Expected :')
      @self.advance
      @self.consumeWhitespace

      @self.parse_value
      @result.set(key,self.last)
      first = false

    @self.advance

  elif self.matches("true")
    @self.consume("true")
    result = true

  elif self.matches("false")
    @self.consume("false")
    result = false

  elif self.matches("null")
    @self.consume("null")
    result = nil

  elif is_digit(self.next) or self.next == '-'

    p = self.pos

    @self.advance
    while is_digit(self.next)
      @self.advance

    foundDot = false
    if self.next == "."
      foundDot = true
      @self.advance

      while is_digit(self.next)
        @self.advance

    -- TODO: exponent notation

    result = self.str.slice(p, self.pos)
    assert(is_string(result))

    if foundDot
      @result.to_number
    else
      @result.to_int

  elif self.next == '"'
    @self.parse_string
    result = self.last

  self.last = result

def parse(String str) -> any
  reader = Reader.make(str 0)
  @reader.parse_value
  return reader.last

def quote(String s) -> String
  -- TODO: insert escapes as needed
  str('"' s '"')

def write(val) -> String
  if is_list(val)
    str('[' (for el in val { write(el) }).join(', ') ']')
  elif is_table(val)
    str('{' (for k in val.keys { str(quote(k), ": ", write(val.get(k))) }).join(', ') '}')
  elif is_string(val)
    quote(val)
  else
    str(val)
