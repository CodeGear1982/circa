
background(#fff)


def draggable_point(Point initial_location, number radius) -> Point
    state Point location = initial_location
    state currently_dragging = false
    state Point mouse_offset = [0 0]
    if currently_dragging
        location = (mouse + mouse_offset) -> Point
        if not(mouse_pressed())
            currently_dragging = false
    elif mouse_clicked() and magnitude(location - mouse) < radius
        currently_dragging = true
        mouse_offset = location - mouse
    return location

points = [[50 200] [50 50] [200 50] [200 200]]

for p in @points
    Point(@p)
    draggable_point(@p, 6.0)

for p in points
    Point(@p)
    draw_circle(p 3.0 #000)

# grey line between control points
line(points[0] points[1] #aaa8)
line(points[2] points[3] #aaa8)

# draw the actual curve
segments = 40
    
segment_locations = []
for s in 0..(segments+1)
    t = s / segments
    p = bezier4(t points)
    segment_locations.append(p)
    
for s in 0..segments
    line(segment_locations[s] segment_locations[s+1] #f00)


