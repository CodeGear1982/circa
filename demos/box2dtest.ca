
include('shared/drifting_stars.ca'->rpath)

def draw_body_outline(any handle, Color color)
    points = box2d:get_body_points(handle)
    gl:line_loop(points color)

def wall(Point p, number rot)
    body = box2d:static_rect(p rot [60 10])
    draw_body_outline(body #fff)

    -- Draggable, using code feedback
    state currently_dragging = false
    if mouse_clicked() and box2d:body_contains_point(body, mouse)
        currently_dragging = true
    elif not(mouse_pressed())
        currently_dragging = false
    if currently_dragging
        state Point mouse_offset = p - mouse
        feedback(p, mouse + mouse_offset)

        -- Rotate with z/x keys
        if key_down('z')
            feedback(rot rot - 0.01)
        elif key_down('x')
            feedback(rot rot + 0.01)

def bouncy_box(Point start)
    box = box2d:dynamic_rect(start 0.0 [3 3] [1 .1 1])
    draw_body_outline(box #fff)

def large_box(Point start)
    body = box2d:dynamic_rect(start 0.0 [10 10] [1 .1 1])
    draw_body_outline(body #fff)


box2d:step()
box2d:gravity([0 8])

large_box([400 100])

for i in 0..100
    bouncy_box([420 100])


wall([520.0 319.0] -0.15)
wall([377.0 234.0] 0.12)
