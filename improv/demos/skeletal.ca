
env = input() -> ScriptContext
mouse = env.mouse

env.background(#000)

type JointDef {
    Point rel
    int parent
    List children
}

type SkeletonDef {
    List joints
}

type JointPos {
    number rotation
}

def SkeletonDef.size(self) -> int
    self.joints.length
def SkeletonDef.addJoint(@self, Point rel, int parent)
    self.joints.append(JointDef([rel parent []]))
def SkeletonDef.getPivots(@self) -> List
    pivots = []
    for JointDef joint in self.joints
        if joint.parent == -1
            pivot = joint.rel
        else
            pivot = pivots[joint.parent] + joint.rel

        pivots.append(Point(pivot))

    pivots

state SkeletonDef skel
state int selectedJoint

pivots = skel.getPivots()

-- Handle input, figure out action
action = :none

if env.mouse_pressed and skel.size == 0
    action = :add_joint
elif env.mouse_pressed
    action = :select
elif env.key_pressed(:a)
    action = :add_joint
elif env.mouse_down and selectedJoint != -1
    action = :drag

if action == :add_joint
    if skel.size == 0
        -- No joints yet: create one
        skel.addJoint(env.mouse, -1)
        selectedJoint = 0
    else
        -- Add joint
        parent = selectedJoint
        parent_pivot = pivots[parent]
        skel.addJoint(env.mouse - parent_pivot, parent)

elif action == :select
    
    closestDistance = 0.0

    closest = -1
    selectDistance = 10.0

    index = 0
    for Point pivot in pivots
        dist = point_distance(pivot env.mouse)

        if dist < selectDistance and (closest == -1 or dist < selectDistance)
            closest = index
            closestDistance = dist
        index += 1

    selectedJoint = closest
elif action == :drag
    skel.joints[selectedJoint].rel += env.mouse.delta

pivots = skel.getPivots()

-- Draw
index = 0
for JointDef joint in skel.joints
    pivot = pivots[index] -> Point
    size = 4
    if index == selectedJoint
        color = #fff
        size = 6
    else
        color = #ff0
    env.fill_rect(pivot.to_rect_center([size size]) color)
    if joint.parent != -1
        parent_pivot = pivots[joint.parent]
        env.draw_line(pivot parent_pivot #ff0)
    index += 1
