
{-
  CodeBrowser v1

  Goals
    Display code as near-text
    Support syntax highlight
    Support independent term position (for animation)
    Support filters

    positionMap( Term -> Rect )
    update_position(FilteredBranch) -> PositionMap

    BrowserView
      positionMap
      Branch branch
      List filters

    BrowserView.draw(painter)

-}

env = input() -> Environment
env.background(#448)

type PositionedPhrase {
    Term term
    List position
    String str
}

type CodeBrowser {

    Branch branch

    List filters
    List terms

    int scrollStart

    List positionedPhrases

    Rect windowPosition
}

def CodeBrowser.updateLayout(@self)
    
    phrases = []
    position = [0 0]

    visibleCount = self.windowPosition.height // 20

    for int index in (self.scrollStart .. (self.scrollStart+visibleCount))
        term = self.branch.get_term(index)

        for phrase in term.format_source()
            str = phrase[0] -> String
            phrase_term = phrase[1]
            id = phrase[2]

            positioned = PositionedPhrase([term position str])
            phrases.append(positioned)

            -- Advance 'position', taking newlines into account
            if id == :tok_Newline
                position = [0, position[1] + 1]
            else
                position[0] += str.length

        position = [0 position[1] + 1]
        position[1] += 1

    self.positionedPhrases = phrases

def CodeBrowser.setPosition(@self, Rect rect)
    if self.windowPosition != rect
        self.windowPosition = rect
        self.updateLayout

def CodeBrowser.draw(self, Painter painter)

    painter.fillRect(self.windowPosition #ccc)

    -- Enable a fixed width font
    painter.setFont(font('assets/jackinput.ttf', 18))

    -- Find the font's dimensions
    letterSize = [painter.currentFont.width('AAAAAAAAAA') / 10, painter.currentFont.height] -> Point

    for PositionedPhrase phrase in self.positionedPhrases
        term = phrase.term
        pos = phrase.position

        screenPos = [pos[0] * letterSize.x, pos[1] * letterSize.y] -> Point
        add(@screenPos, self.windowPosition.topleft)

        Point(@screenPos)
        
        painter.text(screenPos phrase.str #000 [:NoWrap])
        

def make_code_browser(Branch branch) -> CodeBrowser
    browser = create(CodeBrowser)
    browser.branch = branch
    browser.terms = branch.terms
    browser.scrollStart = 0
    browser.updateLayout()
    return browser


script = load_script('intro.ca' -> rpath)
print('script is: ' script)
browser = make_code_browser(script)
browser.setPosition([10, 50, env.bounds.x - 10, env.bounds.y - 10])
browser.draw(env.painter)
