
type Button {
    String label
    any action
    Rect rect
    bool rollover
    bool press
    bool triggered
    bool toggled
}

button_text_color = #fff

def Point.inside(self, Rect rect) -> bool
    return (self.x >= rect.x1) and (self.x <= rect.x2) and (self.y >= rect.y1) and (self.y <= rect.y2)

def button(String label, Rect rect) -> Button
    b = create(Button)
    b.label = label
    b.rect = rect
    return b

def Button.draw(b, Painter p)
    button_color = #afa0d9

    if b.press
        darken(@button_color, .1)
    elif b.toggled
        darken(@button_color, .2)
    elif b.rollover
        lighten(@button_color, .1)

    p.fillRect(b.rect, button_color)

    p.setFont(create_font('Sans Serif', 16))
    p.setPen(create_pen(#fff, 1.0, :none))
    p.drawText(b.rect, AlignHCenter + AlignVCenter, b.label)

def Button.input(b :out, InputEvent e)
    if e.is_mouse
        b.rollover = e.mousePos.inside(b.rect)
        b.triggered = b.rollover and b.press and e.eventType == MouseUp
        b.press = b.rollover and (e.eventType == MouseDown)

type ButtonBar {
    List buttons
}

def button_bar(List definitions) -> ButtonBar
    buttons = for defi in definitions
        b = create(Button)
        b.label = defi[0]
        b.action = defi[1]
        b
    return [buttons]

def ButtonBar.arrange(self :out, Rect rect, Name orientation)
    spacer = 10

    count = self.buttons.length

    if orientation == :horizontal
        eachWidth = (rect.width - spacer * (count - 1)) / count

        left = rect.x1
        buttons = self.buttons
        for button in @buttons
            button.rect = [left, rect.y1, left+eachWidth, rect.y2]
            left += eachWidth + spacer

        self.buttons = buttons
    else
        eachHeight = (rect.height - spacer * (count - 1)) / count

        top = rect.y1
        buttons = self.buttons
        for button in @buttons
            button.rect = [rect.x1, top, rect.x2, top + eachHeight]
            top += eachHeight + spacer

        self.buttons = buttons

def ButtonBar.draw(b, Painter p)
    for button in b.buttons
        button.draw(p)

def ButtonBar.input(b :out, InputEvent e) -> any
    action = null
    buttons = b.buttons
    for button in @buttons
        Button(@button)
        button.input(e)

        if action == null and button.triggered
            action = button.action

    b.buttons = buttons
    return action

def ButtonBar.setToggled(b :out, int index, bool toggle)
    b.buttons[index].toggled = toggle
def ButtonBar.setLabel(b :out, int index, String label)
    b.buttons[index].label = label
