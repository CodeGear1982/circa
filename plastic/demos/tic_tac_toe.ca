
desired_window_size = [400 400]

background_color = #10a
background(background_color)

board_center = [150 150] : Point
square_size = 70

def draw_x(Point loc)
    thickness = 4.0 -- linear_fadein(5.0) * 4.0
    dist = square_size * 0.35
    draw_thick_line(loc - [dist dist], loc + [dist dist], thickness, #fff)
    draw_thick_line(loc + [-dist dist], loc + [dist -dist], thickness, #fff)
end
def draw_o(Point loc)
    radius = square_size * .35
    gl.circle(loc, radius, #fff)
    gl.circle(loc, radius - 6, background_color)
end

-- Draw the board
board_line_thickness = 5.0
board_left = board_center.x - square_size*3/2
board_top = board_center.y - square_size*3/2
board_right = board_center.x + square_size*3/2
board_bot = board_center.y + square_size*3/2

draw_thick_line([board_left + square_size, board_top],
    [board_left + square_size, board_bot], board_line_thickness, #fff)
draw_thick_line([board_right - square_size, board_top],
    [board_right - square_size, board_bot], board_line_thickness, #fff)
draw_thick_line([board_left, board_top + square_size],
    [board_right, board_top + square_size], board_line_thickness, #fff)
draw_thick_line([board_left, board_bot - square_size],
    [board_right, board_bot - square_size], board_line_thickness, #fff)

namespace player
    none = 0
    x = 1
    o = 2
end

type BoardSpace {
    int owner
    int x
    int y
    Point center
}

def initialize_board_spaces() : List
    board_spaces = []
    for x in range(3)
        for y in range(3)
            board_spaces.append([player.none x y
                [board_left + (x+.5) * square_size, board_top + (y+.5) * square_size]])
        end
    end
    return board_spaces
end

state board_spaces = initialize_board_spaces()

state int current_player = player.x

def player_name(int p) : string
    if p == player.x return "X"
    elif p == player.o return "O"
    else return "none" end
end

def other_player(int p) : int
    if p == player.x return player.o
    else return player.x end
end

type WinnerInformation {
    int player
    Point line_a
    Point line_b
}

def get_winner(List board_spaces) : WinnerInformation
    def get_space(int x, int y) : BoardSpace
        return board_spaces[x * 3 + y]
    end

    def is_winner_across_line(List positions) : int
        pos_0 = positions[0] : Point_i
        all_match = true
        start_space = get_space(pos_0.x pos_0.y)
        this_player = start_space.owner
        for pos in positions
            pos = pos : Point_i
            this_space = get_space(pos.x pos.y)
            if this_player != this_space.owner
                all_match = false
            end
        end
        if all_match
            return this_player
        else
            return player.none
        end
    end

    result = WinnerInformation()

    for i in range(3)
        i = i : int
        horizontal_winner = is_winner_across_line([[i 0] [i 1] [i 2]])
        vertical_winner = is_winner_across_line([[0 i] [1 i] [2 i]])
        if horizontal_winner != player.none
            line_x = board_left + (i+.5) * square_size
            result = [horizontal_winner [line_x board_top] [line_x board_bot]]
        elif vertical_winner != player.none
            line_y = board_top + (i+.5)*square_size
            result = [vertical_winner, [board_left line_y] [board_right line_y]] 
        end
    end

    diagonal_winner_tl = is_winner_across_line([[0 0] [1 1] [2 2]])
    diagonal_winner_tr = is_winner_across_line([[2 0] [1 1] [0 2]])

    if diagonal_winner_tl != player.none
        result = [diagonal_winner_tl [board_left board_top] [board_right board_bot]]
    end
    if diagonal_winner_tr != player.none
        result = [diagonal_winner_tr [board_left board_bot] [board_right board_top]]
    end

    return result
end


chosen_space = []

state moves_made = 0
state game_is_finished = false

-- Draw each playable space
for board_space in board_spaces
    board_space = board_space : BoardSpace

    loc = [board_space.x * square_size + board_left, board_space.y * square_size + board_top]
    rect = [loc[0] loc[1] loc[0] + square_size loc[1] + square_size]

    if board_space.owner == player.x
        draw_x(board_space.center)
    elif board_space.owner == player.o
        draw_o(board_space.center)
    end

    playable = board_space.owner == player.none and not(game_is_finished)

    if playable and mouse_over(rect)
        draw_box(rect, #fff4)
    end

    if playable and mouse_clicked(rect)
        chosen_space = board_space
    end
end

state winner_information = WinnerInformation()

if chosen_space != []
    moves_made += 1

    chosen_space = chosen_space : BoardSpace

    -- Modify board_spaces in a silly way b/c the proper way doesn't work yet
    def update_board_spaces(BoardSpace space) : BoardSpace
        if space.x == chosen_space.x and space.y == chosen_space.y
            space.owner = current_player
        end
        return space
    end
    board_spaces = map(update_board_spaces, board_spaces)
    chosen_space = chosen_space : BoardSpace
    current_player = other_player(current_player)

    -- Check if a player has won
    winner = get_winner(board_spaces)

    if winner.player != player.none
        winner_information = winner
    end
end

def draw_thick_line_animated(Point a, Point b, number thickness, Color color, number time)
    length = magnitude(b - a)
    n = norm(b - a)
    actual_b = a + n * linear_fadein(time) * length
    draw_thick_line(a,actual_b,thickness,color)
end

if winner_information.player != player.none
    draw_thick_line_animated(winner_information.line_a winner_information.line_b, 5.0, #fff, 1.0)
    draw_text(ui_font_medium, concat(player_name(winner_information.player) " wins!"),
        #fff, [20 340])
    game_is_finished = true
elif moves_made == 9
    draw_text(ui_font_medium, "Draw!", #fff, [20 340])
    game_is_finished = true
else
    draw_text(ui_font_medium, concat("Current player: " player_name(current_player)), #fff, [20 320])
end
