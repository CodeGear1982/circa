
-- Wrap up mouse position into a point (values are passed in from input.cpp)
state Point mouse

def mouse_in(Box b) : bool
  return (b.x1 < mouse.x) && (b.x2 > mouse.x) && (b.y1 < mouse.y) && (b.y2 > mouse.y)
end

-- Information about the current window
type Window {
   int width
   int height
}

window = Window()

-- Text rendering functions
namespace text
    --type +native TTF_Font
    type RenderedText { int texid, float width, float height, Color color, string text }
    def load_font +native (state TTF_Font, string, int) : TTF_Font
    def render_text +native (state RenderedText, TTF_Font, string, Color color) : RenderedText
    def draw_rendered_text +native (RenderedText, float x, float y)
end

def draw_text(TTF_Font font, string str, Color color, Point location)
    sprite = text.render_text(font, str, color)
    text.draw_rendered_text(sprite, location.x, location.y)
end

-- GL functions
def background +native (Color)
namespace gl
    def triangles +native (List, Color)
    def line_strip +native (List, Color)
    def line_loop +native (List, Color)
    def points +native (List, Color)
    def circle +native (Point loc, float radius, Color)
end

def load_texture +native (state int, string) : int
def draw_image +native (state int texid, string filename, float,float,float,float)

def load_mesh +native (string) : int
def draw_mesh +native (int, int, List, List, List)

-- polar: polar to cartesian
def polar(float angle, float radius, Point center) : Point
  return [cos(angle) * radius + center.x, sin(angle) * radius + center.y]
end

-- magnitude of a 2d vector
def magnitude(Point point) : float
    return sqrt(sqr(point.x) + sqr(point.y))
end

def draw_box(Box box, Color color)
    nw = [box.x1 box.y1]
    ne = [box.x2 box.y1]
    sw = [box.x1 box.y2]
    se = [box.x2 box.y2]
    gl.triangles([nw ne se se sw nw] color)
end

ui_font_medium = text.load_font("assets/sv_basic_manual/SVBasicManual.ttf", 16)

namespace fps_counter
    state int framesElapsed = 0
    state float timeStarted = time
    state float lastRecoredFps = 0

    framesPerSample = 30
    earlySampleTime = .5

    framesElapsed += 1
    timeElapsed = time - timeStarted

    if (framesElapsed > framesPerSample) || (timeElapsed > earlySampleTime)
        lastRecoredFps = framesElapsed / timeElapsed
        framesElapsed = 0
        timeStarted = time
    end
end

fps = fps_counter.lastRecoredFps

namespace tweak
    def button(Ref r, Point loc, string name)
        str = concat(name ' = ' r.to_string)

        text_sprite = text.render_text(ui_font_medium, str, #000)
        padding = 3.0
        rect = [loc.x - padding, loc.y - padding,
            loc.x + text_sprite.width + padding,
            loc.y + text_sprite.height + padding]
        color = #ee3399
        if mouse_in(rect) color = #ff44aa end
        draw_box(rect, color)
        text.draw_rendered_text(text_sprite, loc.x, loc.y)

        -- mouse wheel
        if mouse_wheel_up(rect)
            r.tweak(-1)
        elif mouse_wheel_down(rect)
            r.tweak(1)
        end

        -- click and drag
        state mouse_dragging = false
        state Point mouse_drag_previous_point = [0 0]
        if mouse_clicked(rect)
            mouse_dragging = true
            mouse_drag_previous_point = mouse
        elif not(mouse_pressed())
            mouse_dragging = false
        end

        if mouse_dragging
            delta = (mouse - mouse_drag_previous_point) : Point
            mouse_drag_previous_point = mouse
            steps = round(delta.y)
            r.tweak(steps)
        end
    end
end

-- Load the user's script
state string user_script_filename
users_branch = include(user_script_filename)

-- Draw HUD

-- Draw fpses
--text_sprite = text.render_text(ui_font_medium, concat('fps: ' fps), #fff)
--text.draw_rendered_text(text_sprite, 10 280)

-- Utility function, might be upgraded to standard lib
def toggle(bool tog) : bool
  state bool on = false
  if tog; on = not(on); end
  return on
end

-- Name selector
if toggle(key_pressed(KEY_B))
  users_branch_ref = &users_branch
  branch_inspector = [users_branch_ref] : BranchInspector
  x = 10, y = 10
  for r in branch_inspector.get_configs()
    tweak.button(r, [x y], branch_inspector.get_relative_name(r))
    y += 24
  end
end

