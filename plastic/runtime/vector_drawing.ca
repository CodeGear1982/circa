
def fill_rect(Rect rect, Color color)
    "Draw a filled rectangle."
    nw = [rect.x1 rect.y1]
    ne = [rect.x2 rect.y1]
    sw = [rect.x1 rect.y2]
    se = [rect.x2 rect.y2]
    gl:triangles([nw ne se se sw nw] color)

def line(Point x, Point y, Color color)
    "Draw a line."
    gl:line_strip([x y] color)

def draw_rect(Rect rect, Color color)
    gl:line_strip([[rect.x1 rect.y1]
        [rect.x2 rect.y1]
        [rect.x2 rect.y2]
        [rect.x1 rect.y2]
        [rect.x1 rect.y1-1]] color)

def thick_line(Point a, Point b, number width, Color color)
    "Draw a thick line with the given thickness."
    perp = perpendicular(norm(a - b)) * width
    gl:triangles([a - perp, a + perp, b + perp, b+perp, b-perp, a-perp] color)

def draw_circle(Point p, number radius, Color color)
    -- Dumb guess on how many points to use
    steps = 20

    points = for step in 0..steps
        [cos(360* step / steps), sin(360 * step / steps)] * radius + p

    gl:line_loop(points, color)

def fill_rounded_rect(Rect rect, number cornerRadius, Color color)
    "Draw a rectangle with rounded corners"
    width = rect.x2 - rect.x1
    height = rect.y2 - rect.y1
    r = cornerRadius
    min(@r, width/2)
    min(@r, height/2)

    -- top left
    gl:pie([rect.x1 + r, rect.y1 + r] r .75 1.0 color)

    -- top right
    gl:pie([rect.x2 - r, rect.y1 + r] r 0 .25 color)

    -- top
    fill_rect([rect.x1 + r, rect.y1, rect.x2 - r, rect.y1 + r] color)

    -- left
    fill_rect([rect.x1, rect.y1 + r, rect.x1 + r, rect.y2 - r] color)

    -- middle
    fill_rect([rect.x1+r,rect.y1+r, rect.x2 - r,rect.y2 - r] color)

    -- right
    fill_rect([rect.x2-r, rect.y1 + r, rect.x2, rect.y2 - r] color)

    -- bottom left
    gl:pie([rect.x1+r, rect.y2 - r] r .5 .75 color)

    -- bottom 
    fill_rect([rect.x1 + r, rect.y2-r, rect.x2 - r, rect.y2] color)

    -- bottom right
    gl:pie([rect.x2-r, rect.y2-r] r .25 .5 color)
